plugins {
    id 'java'
    id "io.freefair.lombok" version "8.6"
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'io.freefair.lombok'

    group = 'io.github.uladzimirfilipchanka'
    sourceCompatibility = '17'

    dependencies {
        implementation 'org.apache.commons:commons-lang3:3.14.0'
        implementation 'org.slf4j:slf4j-api:1.7.25'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    artifacts {
        archives javadocJar, sourcesJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'io.github.uladzimirfilipchanka'
                artifactId = project.name
                version = '0.0.1'

                from components.java

                pom {
                    name = project.name
                    url = 'https://github.com/uladzimirFilipchanka/noredraw'
                    inceptionYear = '2024'
                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://spdx.org/licenses/Apache-2.0.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'uladzimirFilipchanka'
                            name = 'Vladimir Filipchanka'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/uladzimirFilipchanka/noredraw.git'
                        developerConnection = 'scm:git:ssh://github.com:uladzimirFilipchanka/noredraw.git'
                        url = 'https://github.com/uladzimirFilipchanka/noredraw.git'
                    }
                }
            }
        }

        repositories {
            maven {
                url "$project.buildDir/staging"
//                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
//                credentials {
//                    username ossrhUsername
//                    password ossrhPassword
//                }
            }
        }
    }

    signing {
        sign configurations.archives
        sign publishing.publications.maven
    }

    tasks.publishMavenPublicationToMavenLocal {
        dependsOn(project.tasks.signArchives)
    }
    tasks.publishMavenPublicationToMavenRepository {
        dependsOn(project.tasks.signArchives)
    }
}
